buildscript {
	ext {
		springBootVersion = '1.4.2.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'

jar {
	baseName = 'micro-todo-member'
	version = '0.0.1-SNAPSHOT'
}
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
	mavenCentral()

	// For ApiStubGenerator
	flatDir(name: "project-libs", dirs: "libs")
}

dependencies {
	compile('org.springframework.boot:spring-boot-starter-actuator')
	compile('org.springframework.cloud:spring-cloud-starter-config')
	compile('org.springframework.boot:spring-boot-starter-data-jpa')
	compile('org.springframework.boot:spring-boot-starter-data-rest')
	compile('org.springframework.boot:spring-boot-starter-web')

	// For ApiStubGenerator - start
	compile files('libs/spring-web-api-test-stubber-1.0-SNAPSHOT.jar')
	compile group: 'com.squareup', name: 'javapoet', version: '1.8.0'
	compile group: 'junit', name: 'junit', version: '4.11'
	compile group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: '1.4.2.RELEASE'
	// For ApiStubGenerator - end

	runtime('com.h2database:h2')
	compileOnly('org.projectlombok:lombok')
	testCompile('org.springframework.boot:spring-boot-starter-test')
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:Camden.SR2"
	}
}

// For ApiStubGenerator - start
sourceSets {
	generated {
		java {
			srcDirs = ['src/test/java']
		}
	}
}

task generateApiTestStub(type: JavaCompile, group: 'build') {
	source = sourceSets.main.java.srcDirs
	classpath = configurations.compile
	options.compilerArgs = [
	        "-proc:only",
			"-processor", "homo.efficio.spring.web.api.test.stubber.processor.RestControllerProcessor"
	]
	destinationDir = sourceSets.generated.java.srcDirs.iterator().next()
}

compileJava {
	dependsOn generateApiTestStub
}
// For ApiStubGenerator - end
